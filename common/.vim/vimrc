" Reference: 참고 자료 및 중요 키  {{{
" -------------------
" Global:
" :o openfile, :saveas files, :close, :q, :wq
" :sh goto terminal, :termianl open new ternial in horizontal split
" :noh remove highlight

" Start Edit:
" i(a) - insert before(after) cursor
" I(A) - insert begining(end) of line
" d - diw(delete inside), d%(mathing braces), dd(delete line)
" c - ciw(delete and insert mode), cw, cc(delete line and start editing)

" Tab Edit:
" :tabedit, :tabfirst, :tablast, :tabn(next), :tabp(previous), :tabnext 1(goto 1st tab)
" :tabclose, :tabonly, gt(next tab), gT(previous tab), 2gt(second tab)
" :mksession header-files-work.vim or :source header-files-work.vim,
" :mks!(if changed session tab :mks! to store)

" Cursor Movement:
" 0 (start), $(end) of the line
" ^(g_) first(last)  non-block character of the line
" % jumpt to the next parenthesis or bracket

" Marking:
" mk - move to end of block - y'k(copy block), d'k(delete block)
" mb - opening brace position - %(curse to close brace position) - zf'b
" (folding selected b block)
" m{a-z,A-z} - :marks, goto mark '+mark
" mP - 'P(대문자의 경우 global: 해당 위치로 커서 이동) `P(begining of file)

" Folding:
" :5,26fo (5th to 26th line folding), zo(open the code fold), za(cloose the
" code fold), za(open and close)
" 폴딩 설정 저장: ~/.vimrc에 아래 두라인을 추가
" autocmd BufWinLeave *.* mkview
" autocmd BufWinEnter *.* silent loadview

" Code Indentation:
" :=i{ - i(inner object) : 커서를 블록 내 위치 시킨 후 normal mode에서 =i{
" increase/decrease indentation - :>i(increase), :<i(decrease)

" Explorer Directory:
" :Ex(plore), :Sexplore, Vexplore, Texplore(tab),
"
" Buffers:
" :bnext(:bn), bprev(:bp), :bdel(buffer delete)
" :ls - list all open buffers

" Macro:
" q{register}(start macro named a) - do something - q(end recording macro) - @a(excute
" recorded marcro a)
" :reg - see values of every registers

" Remote SSH Editing:
" vim scp://user@remote/relative/path/of/file
" vim scp://remotehost-ssh.config/path

" Searc Multiple Files:
" :vimgrep /pattern/ {file}
" :cn(:cp) next(previous) match
" :copen - open a window containing the list of matches

" Exiting:
" :w
" :w !sudo tee %A
"
" }}}

" General Settings: {{{

if has('termguicolors')
    set termguicolors
endif

set nocompatible                                        " make vim more powerful
filetype plugin indent on
filetype plugin on
syntax on
set mouse=a                                             " support mouse
set confirm                                             " ask before unsage action

" ssh 사용시 발생할 수 있는 문제 예방
set noswapfile nobackup                                 " don't use swap file

" undo 관련된 설정
if !isdirectory($HOME."/.vim")
    call mkdir($HOME."/.vim", "", 0770)
endif
if !isdirectory($HOME."/.vim/undo-dir")
    call mkdir($HOME."/.vim/undo-dir", "", 0700)
endif
set undodir=$HOME/.vim/undo-dir                         " undo-dir 설정
set undofile

set encoding=utf-8
set clipboard=unnamed,unnamedplus                       " 외부 clipboard 이용
set number relativenumber                               " with relativenumber, show current number

set showcmd
set noshowmode
set wildmenu
set wildmode=longest,list,full

set smarttab tabstop=4 softtabstop=4 shiftwidth=4       " width of a tab character
set smartindent
set expandtab

set ignorecase smartcase
set incsearch hlsearch
set gdefault
set ruler laststatus=2 showcmd showmode
set list listchars=trail:»,tab:»-
set fillchars+=vert:\
set nowrap breakindent
set title
set backspace=indent,start,eol                           " <BS> 역할설정, eol 제외
set scrolloff=8                                          " during scroll keep 3 lines be shown"
set laststatus=2                                         " always show status line
set cursorline                                           " highlight the line with a cursor
set list
set listchars=tab:▸\ ,trail:⋅,extends:>,precedes:<,eol:¬,nbsp:_

" Disable cursor line highlighting in Insert mode
augroup aug_cursor_line
   au!
   au InsertEnter * setlocal nocursorline
   au InsertLeave * setlocal cursorline
augroup END

"}}}

" Custom Key Mappings: {{{
" Set <leader> key to <Space>
nnoremap <Space> <Nop>
let mapleader=" "
let maplocalleader=","

" iPad를 위해 <ESC>를 키 매핑, 무척 편리함. set paste와 같이 쓸 경우 안됨.
inoremap jk <ESC>
inoremap ㅓㅏ <ESC>
" 긴줄의 경우 j k 키가 원하지 않게 움직이는 것을 교정
noremap k gk
noremap j gj

" ale
if exists('g:loaded_ale')
  call ale#linter#Define('markdown', {
  \ 'name': 'marksman',
  \ 'lsp': 'stdio',
  \ 'executable': '/opt/homebrew/bin/marksma',
  \ 'command': '%e server',
  \ 'initialization_options': {},
  \ })
endif

" }}}

" AutoComplPop: keymapping {{{
" 타이핑을 시작하면 자동으로 팝업메뉴가 떠서 선택할 수 있는 플러그인
" Navigate the complete menu items like CTRL+n / CTRL+p would.
inoremap <expr> <Down> pumvisible() ? "<C-n>" :"<Down>"
inoremap <expr> <Up> pumvisible() ? "<C-p>" : "<Up>"
" Select the complete menu item like CTRL+y would.
inoremap <expr> <Right> pumvisible() ? "<C-y>" : "<Right>"
inoremap <expr> <CR> pumvisible() ? "<C-y>" : "<CR>"
" Cancel the complete menu item like CTRL+e would.
inoremap <expr> <Left> pumvisible() ? "<C-e>" : "<Left>"
" }}}

" Plugins Install: {{{

call plug#begin()

    " Vim Make Pretty:
    Plug 'catppuccin/vim', { 'as': 'catppuccin' }
    Plug 'junegunn/goyo.vim'
    Plug 'junegunn/limelight.vim'

    " Vim Make Friendly:
    Plug 'vim-airline/vim-airline'
    Plug 'vim-airline/vim-airline-themes'
    " Plug 'mhinz/vim-startify'                     " fancy start screen for vim 

    " Shell:
    Plug 'scrooloose/nerdtree'
    Plug 'samoshkin/vim-lastplace'                  " 파일을 열때 마지막 상태로 불러들임
    Plug 'vim-scripts/CursorLineCurrentWindow'      " cursor의 위치를 highlight로 보여줌 

    " Search:
    Plug 'junegunn/fzf', { 'do': { -> fzf#install() } }
    Plug 'junegunn/fzf.vim'
    Plug 'junegunn/vim-slash'                       " 검색된 결과를 강조(highlight), 커서가 벗어나면 강조해제

    " GIT:
    Plug 'tpope/vim-fugitive'                       " git plugin
    Plug 'junegunn/gv.vim'                          " git commit browser :GV | GV! | GV?
    Plug 'airblade/vim-gitgutter'                   " show git diff in the 'gutter' (sign colume)

    " Keyboard:
    Plug 'mg979/vim-visual-multi', {'branch': 'master'}
                                                    " C-n | n N q, check :help visual-multi 
    Plug 'tpope/vim-commentary'                     " comment 처리
    Plug 'tpope/vim-surround'
        " surroundings: parentheses, brackets, quotes, XML tabs and more
        " ys (add), ds (delete), cs (change)
        " ysiw) --> (word), 0ys$" --> 해당라인 "" 혹은 yss"도 마찬가지결과 ex) yssb 한줄을 ()로 둘러쌈 b=brakets 
        " cs"' : " --> '
        " or yss)|ysiw<em>|visual mode S<p class="important">
    Plug 'jiangmiao/auto-pairs'                 " pair brackets, parens, quotes
    Plug 'vim-scripts/AutoComplPop'             " :help AutoComplPop 
    Plug 'christoomey/vim-tmux-navigator'       " navation between vim and tmux splits

    " Snippets:  
    Plug 'SirVer/ultisnips'                     " <tab>,<s-tab>으로 stop간 이동
    Plug 'honza/vim-snippets'                   " 미리 설정된 snippets를 활용 가능, 마크다운도 있음
 
    "  Markdown:
    " Plug 'plasticboy/vim-markdown'              " syntax highlighting matching rules and mappings for the original Markdown and extensions
    Plug 'dense-analysis/ale'
    Plug 'MattesGroeger/vim-bookmarks'
        " before installation, cehck :echo has('signs') 결과 1이면 OK!

call plug#end()

" }}}

" Simple Plugin Configurations: {{{

set background=dark
colorscheme catppuccin_macchiato
" colorscheme catppuccin_frappe

" AutoComplPop: 관련 설정
set complete+=kspell
set completeopt=menuone,longest
set shortmess+=c

" Airline:
" let g:airline_theme = 'sonokai'
let g:airline_powerline_fonts = 1
let g:airline_section_z = ' %{strftime("%-I:%M %p")}'
let g:airline_section_warning = ''

" LimeLight: 
nmap <Leader>l <Plug>(Limelight)
xmap <Leader>l <Plug>(Limelight)

" NerdTree:
noremap  <leader>ee :NERDTreeToggle<CR>
nnoremap <leader>ef :NERDTreeFind<CR>

" GitGutter:
nmap <Leader>g :GitGutterToggle<CR>

" Bookmars:
" unbind the keys whenever Nerdtree becomes active
let g:bookmark_no_default_key_mappings = 1
function! BookmarkMapKeys()
    nmap mm :BookmarkToggle<CR>
    nmap mi :BookmarkAnnotate<CR>
    nmap mn :BookmarkNext<CR>
    nmap mp :BookmarkPrev<CR>
    nmap ma :BookmarkShowAll<CR>
    nmap mc :BookmarkClear<CR>
    nmap mx :BookmarkClearAll<CR>
    nmap mkk :BookmarkMoveUp
    nmap mjj :BookmarkMoveDown
endfunction
function! BookmarkUnmapKeys()
    unmap mm
    unmap mi
    unmap mn
    unmap mp
    unmap ma
    unmap mc
    unmap mx
    unmap mkk
    unmap mjj
endfunction
autocmd BufEnter * :call BookmarkMapKeys()
autocmd BufEnter NERD_tree_* :call BookmarkUnmapKeys()
" BookmarkSign
highlight SignColumn ctermbg=232
highlight BookmarkSign ctermbg=NONE ctermfg=216
highlight BookmarkAnnotationSign ctermbg=60 ctermfg=184
highlight BookmarkLine ctermbg=NONE ctermfg=226
highlight BookmarkAnnotationLine ctermbg=232 ctermfg=NONE
" bookmark와 관련된 기본 설정
let g:bookmark_auto_save = 0
let g:bookmark_auto_close = 1
let g:bookmark_highlight_lines = 1
let g:bookmakr_center = 1
" let g:bookmark_display_annotation = 1
" let g:bookmark_save_per_working_dir = 1
" function! g:BMWorkDirFileLocation()
"     return expand('~/Sync/') . '.vim-bookmarks'
" endfunction
let g:bookmark_auto_save = 1
" let g:bookmark_auto_save_file = '~/Sync/.vim-bookmarks'

" 아래의 AddBookmarkRoot()을 활용해 bookmarks를 한곳에서 관리하기 위함
let s:autoload_roots = []
let s:last_root = ''
let s:last_file = ''

function! AddBookmarkRoot(root)
  call add(s:autoload_roots, a:root)
endfunction!

function! AutoloadBookmarks(file_name)
  let root_is_found = 0
  let found_root = 0
  let name_len = strlen(a:file_name)

  for root in s:autoload_roots
    let root_len = strlen(root)
    if (name_len > root_len && strpart(a:file_name, 0, root_len) == root)
      let root_is_found = 1
      let found_root = root
      break
    endif
  endfor

  if (root_is_found && found_root != s:last_root)
    let s:last_file = found_root . '/.bookmarks'
    let s:last_root = found_root
    call BookmarkLoad(s:last_file, 0, 1)

    augroup AutoSaveBookmarks
      autocmd!
      autocmd BufLeave * call s:remove_group()
    augroup END
  else
    let s:last_root = ''
  endif
endfunction

augroup AutoLoadBookmarks
  autocmd!
  autocmd BufEnter * call AutoloadBookmarks(expand("<afile>:p"))
augroup END

function! s:remove_group()
  call BookmarkSave(s:last_file, 1)
  augroup AutoSaveBookmarks
    autocmd!
  augroup END
endfunction
" 위에서 정의된 함수를 호출
autocmd VimEnter * call AddBookmarkRoot('~/Sync')
" }}}

" Fzf_Settings: {{{
" Files (runs $FZF_DEFAULT_COMMAND if defined)
nnoremap <leader>ff :Files<CR>
nnoremap <leader>fF :Files!<CR>
nnoremap <leader>fb :Buffers<CR>
nnoremap <leader>fc :Colors<CR>
nnoremap <leader>fa :Ag<CR>
nnoremap <leader>fr :Rg<CR>
" rg search result; relaunch ripgrep on every keystroke
nnoremap <leader>fR :RG<CR>

" preview windows 관련 설정
" let g:fzf_vim.preview_window = ['hidden,right,50%,<70(up,40%)', 'ctrl-/']

" 사용자 정의 추가, Sync 폴더를 기준으로 검색
command! -bang -nargs=? -complete=dir Sync 
    \ call fzf#vim#files('~/Sync', {'options': ['--layout=reverse', '--info=inline', '--preview', '~/.vim/plugged/fzf.vim/bin/preview.sh {}']}, <bang>0)

" 지정한 폴더를 기준으로 file searching
" command! -bang Sync call fzf#vim#files('~/Sync', <bang>0,)
" command! -bang LogSEQ call fzf#vim#grep('~/LogSEQ-EveryNotes/')
" command! -bang LogSEQ call fzf#vim#grep('ag --nocolor --nogroup ''%s'' ~/LogSEQ-EveryNotes/', 1, <bang>0, <q-args>)

" }}}

" Folding: {{{
" 속도 문제가 발생, 파일오픈 시 unfolded로 불러들임.
set foldenable
set foldmethod=marker
set foldlevelstart=2
" set foldcolumn=1
" What triggers automatic fold opening
set foldopen-=block
set smartindent
set foldopen-=hor
set foldopen+=jump
" Remap folds everything all the way: fold-close-all
nnoremap <leader>za zM
" }}}

" Vim Lastplace: {{{
let g:lastplace_ignore = "gitcommit,gitrebase,svn,hgcommit"
let g:lastplace_ignore_buftype = "quickfix,nofile,help"
" }}} 

" Window Management: {{{
" 적응해서 사용하기: 괜찮은 것 같음
" Navigate between windows
noremap <silent> <C-k> :wincmd k<CR>
noremap <silent> <C-j> :wincmd j<CR>
noremap <silent> <C-h> :wincmd h<CR>
noremap <silent> <C-l> :wincmd l<CR>

" Splits: default splitabove
set splitbelow
set splitright
nnoremap <silent> <leader>_ :split<CR>
nnoremap <silent> <leader>\| :vsplit<CR>
nnoremap <silent> <leader>0 :only<CR>

" Maximize Split: Use '<C-w>=' to make window sizes equal back
" <C-w>+,- : resize window (vertical), <C-w><,> : horizontal
" :resize +N, -N
" :vertical resize +N, -N

" Tab Navigation:
nnoremap <silent> <leader>1 :tabnext 1<CR>
nnoremap <silent> <leader>2 :tabnext 2<CR>
nnoremap <silent> <leader>3 :tabnext 3<CR>
nnoremap <silent> <leader>4 :tabnext 4<CR>
nnoremap <silent> <leader>5 :tabnext 5<CR>
nnoremap <silent> <leader>9 :tablast<CR>

" Tab Management:
nnoremap <silent> <leader>+ :tabnew<CR>:edit .<CR>
nnoremap <silent> <leader>) :tabonly<CR>
nnoremap <silent> <leader>- :tabclose<CR>
" }}}

" Vim Commentary: {{{
" 모든 파일 타입에 대해 <leader>c를 주석 처리 단축키로 설정
autocmd FileType * nnoremap <buffer> <leader>c :Commentary<CR>
autocmd FileType * vnoremap <buffer> <leader>c :Commentary<CR>
" }}}

" Fugitive: {{{
" 몇가지 정상적으로 작동하지 않음. 수정 필요
" View GIT index window
nnoremap <silent> <leader>gs :Gstatus<CR>

" Use <leader>ge to return to working tree version from blob, blame, log
nnoremap <silent> <leader>ge :Gedit<CR>

" Undo changes in working tree
" nnoremap <silent> <leader>gu :Git checkout HEAD -- %:p<CR>
nnoremap <silent> <leader>gu :Gread<CR>
xnoremap <silent> <leader>gu :Gread<CR>

" Commit changes
nnoremap <silent> <leader>gca :Gcommit --all --verbose<CR>
nnoremap <silent> <leader>gcf :Gcommit --amend --reuse-message HEAD<CR>
nnoremap <silent> <leader>gcf :Gcommit --amend --verbose<CR>

" Diff working tree vs index vs HEAD
nnoremap <silent> <leader>gdw :Gdiff<CR>
nnoremap <silent> <leader>gdh :Gdiff HEAD<CR>
nnoremap <silent> <leader>gdi :Gdiff --cached HEAD<CR>

" gla, gva, list (a)ll commits
nnoremap <silent> <leader>gla :Commits<CR>
nnoremap <silent> <leader>gva :GV<CR>

" glf, gvf, list commits touching current (f)ile
nnoremap <silent> <leader>glf :BCommits<CR>
nnoremap <silent> <leader>gvf :GV!<CR>
xnoremap <silent> <leader>gvf :GV<CR>

" gls, gvs, list commits touching current file, but show file revisions or (s)napshots (populates quickfix list)
nnoremap <silent> <leader>gls :silent! Glog<CR><C-l>
nnoremap <silent> <leader>gvs :GV?<CR>

" glF, list commits touching current file, show full commit objects (using vim-fugitive)
nnoremap <silent> <leader>glF :silent! Glog -- %<CR><C-l>

" Change branch
nnoremap <silent> <leader>gco :Git checkout<Space>
"
" }}}

" Startify: {{{
let g:startify_custom_header = [
  \ '   ____  _ _ _                      _   _         ____  _         _ _     _      ',
  \ '  / ___|(_) | |__   ___  _   _  ___| |_| |_ ___  / ___|| |_ _   _| (_)___| |_    ',
  \ '  \___ \| | |  _ \ / _ \| | | |/ _ \ __| __/ _ \ \___ \| __| | | | | / __| __|   ',
  \ '   ___) | | | | | | (_) | |_| |  __/ |_| ||  __/  ___) | |_| |_| | | \__ \ |_    ',
  \ '  |____/|_|_|_| |_|\___/ \__,_|\___|\__|\__\___| |____/ \__|\__, |_|_|___/\__|   ',
  \ '                                                             |___/               ',
  \ '                                                                                 ',
  \ '               __  __       ____        _   _   _                                ',
  \ '               \ \/ /_   _ |  _ \  ___ (_) | | | | _   _   __ _  _ __    __ _    ',
  \ '                \  /| | | || |_) |/ _ \| | | |_| || | | | / _` || `_ \  / _` |   ',
  \ '                /  \| |_| ||  __/|  __/| | |  _  || |_| || (_| || | | || (_| |   ',
  \ '               /_/\_\\__,_||_|    \___||_| |_| |_| \__,_| \__,_||_| |_| \__, |   ',
  \ '                                                                        |___/    ', 
  \ ]

" 'most recent files' number
let g:startify_files_number = 15
let g:startify_session_persistence = 1
let g:startify_lists = [
  \ { 'type': 'files',        'header': ['    Recent Files']  },
  \ { 'type': 'dir',          'header': ['    Recent Files '.  getcwd()]  },
  \ { 'type': 'sessions',     'header': ['    Saved Sessions']  },
  \ { 'type': 'bookmarks',    'header': ['    Bookmarks']  },
  \ ]

function! s:sy_add_bookmark(bookmark)
  if !exists('g:startify_bookmarks')
    let g:startify_bookmarks = []
  endif
  let g:startify_bookmarks += [ a:bookmark ]
  endfunction

command! -nargs=1 StartifyAddBookmark call <sid>sy_add_bookmark(<q-args>)
" }}}

" Ultisnip: {{{
" 실행 하기전 vim --version으로 python을 지원하는지 확인 필요
" Trigger configuration. : 충돌 가능성이 큼
" :verbose imap <tab> 활용 충돌 유무 확인
" You need to change this to something other than
" <tab> if you use one of the following:
" - https://github.com/Valloric/YouCompleteMe
" - https://github.com/nvim-lua/completion-nvim
let g:UltiSnipsExpandOrJumpTrigger="<tab>"
let g:UltiSnipsJumpForwardTrigger="<tab>"
let g:UltiSnipsJumpBackwardTrigger="<s-tab>"
" 위의 <tab>키가 다른 플러그인과 충돌할 경우 
" let g:UltiSnipsExpandTrigger="<leader><tab>"
" let g:UltiSnipsJumpForwardTrigger="<leader><tab>"
" let g:UltiSnipsJumpBackwardTrigger="<leader><s-tab>"
" 를 사용하는 것을 고려해 볼 수 있음, 현재 문제 없이 작동 함.

autocmd FileType vimwiki,markdown exec "UltiSnipsAddFiletypes ps"


" If you want :UltiSnipsEdit to split your window.
let g:UltiSnipsEditSplit="vertical"

" auto-pair와 충동 해결
let g:AutoPairsShortcutJump = ''
let g:AutoPairsShortcutBackInsert = ''

" }}}


" Emoji Shortcuts: {{{
ab :) 😃
ab :check_mark: ✅
ab :warning: ⚠️
ab :bulb: 💡
ab :pushpin: 📌
ab :bomb: 💣
ab :pill: 💊
ab :point_right: 👉
ab :book: 📖
ab :link: 🔗
ab :email: 📨
ab :smile: 😃
ab :cold_sweat: 😰
ab :smile_sweat: 😅
ab :scary: 😱
ab :pencil: ✏️ 
ab :thermometer: 🌡️
ab :speech: 💬
ab :pin: 📍
ab :telescope: 🔭
ab :lollipop: 🍭
ab :barber: 💈
ab :bell: 🔔
ab :prohibited: 🚫
ab :!: ❗
ab :!!: ‼️
ab :sun: 🔆
ab :no: ⛔
ab :chick: 🐥
ab :rain: 🌧️
" }}}


